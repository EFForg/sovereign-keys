import os
import sys

GTEST_DIR = 'third_party/gtest-1.6.0'

env = Environment(CXXFLAGS = '-g -fPIC -Wall -Wextra -pedantic -std=c++0x')
envTest = Environment(CPPPATH = [GTEST_DIR, GTEST_DIR + '/include'],
                      CXXFLAGS = '-g -Wall -Wextra -pedantic -std=c++0x')

envTest.Library(target = 'gtest',
                source = GTEST_DIR + '/src/gtest-all.cc')

envTest.Library(target = 'gtest_main',
                source = GTEST_DIR + '/src/gtest_main.cc')

Alias('swig',
      env.LoadableModule('sk', 'libsk/sk.i',
        SWIGFLAGS = '-c++ -python -shadow -Wall'.split(),
        CPPPATH = [
          'libsk',
          os.path.join(sys.prefix, 'include', 'python%d.%d' % (
            sys.version_info[0], sys.version_info[1]))
          ],
        CCFLAGS = '-O',
        LIBPATH = '.',
        LIBS = ['ssl', 'crypto', 'libsk'],
        LDMODULEPREFIX = '_',
        LDMODULESUFFIX = '.so',
        FRAMEWORKSFLAGS = '-flat_namespace -undefined suppress',
      )
)

env.Library(target = 'libsk',
            source = [
              'libsk/add_root_ca_descriptor.cc',
              'libsk/add_root_ca_entry.cc',
              'libsk/base64.cc',
              'libsk/bind_descriptor.cc',
              'libsk/bind_entry.cc',
              'libsk/blob_value.cc',
              'libsk/boolean_value.cc',
              'libsk/change_services_descriptor.cc',
              'libsk/change_services_entry.cc',
              'libsk/decimal_integer.cc',
              'libsk/descriptor.cc',
              'libsk/integer_value.cc',
              'libsk/key_type_value.cc',
              'libsk/log_entry.cc',
              'libsk/message.cc',
              'libsk/mt_random.cc',
              'libsk/rebind_descriptor.cc',
              'libsk/rebind_entry.cc',
              'libsk/rebinder_names_value.cc',
              'libsk/remove_root_ca_descriptor.cc',
              'libsk/remove_root_ca_entry.cc',
              'libsk/services_value.cc',
              'libsk/sha.cc',
              'libsk/slice.cc',
              'libsk/string_value.cc',
              'libsk/tfm.cc',
              'libsk/tfm_descriptor.cc',
              'libsk/timestamp_value.cc',
              'libsk/unbind_descriptor.cc',
              'libsk/unbind_entry.cc',
              'libsk/value.cc',
              'libsk/varint.cc',
              ])

envTest.Program(target = 'libsk_tests',
                source = Glob('libsk/*_unittest.cc') + [
                  'libsk/test_util.cc'
                  ],
                LIBS = ['libsk',
                        'ssl',
                        'crypto',
                        'pthread',
                        'gtest', 'gtest_main'],
                LIBPATH = '.')
